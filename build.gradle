apply plugin: 'com.android.application'

task wrapper(type: Wrapper) {
    gradleVersion = '3.3'
}

buildscript {
    repositories {
        jcenter()
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:2.3.1'
    }
}

repositories {
    jcenter()
}

android {
    lintOptions {
        lintConfig file("lint.xml")
        textOutput 'stdout'
        textReport true
    }

    compileSdkVersion 25
    buildToolsVersion "25.0.2"

    defaultConfig {
        applicationId "org.tasks"
        versionCode 453
        versionName "4.9.12"
        minSdkVersion 15
        targetSdkVersion 25
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        jackOptions {
            enabled true
        }
    }

    signingConfigs {
        release
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
        incremental false
    }

    buildTypes {
        debug {
            multiDexEnabled true
        }
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard.pro'
            signingConfig signingConfigs.release
        }
    }

    flavorDimensions 'store', 'env'

    productFlavors {
        generic {
            dimension 'store'
        }
        googleplay {
            dimension 'store'
        }
        amazon {
            dimension 'store'
        }
        dev {
            minSdkVersion 21
            dimension 'env'
        }
        prod {
            dimension 'env'
        }
    }

    if (project.hasProperty('keyAlias') &&
            project.hasProperty('storeFile') &&
            project.hasProperty('storePassword') &&
            project.hasProperty('keyPassword')) {
        android.signingConfigs.release.keyAlias = keyAlias
        android.signingConfigs.release.storeFile = file(storeFile)
        android.signingConfigs.release.storePassword = storePassword
        android.signingConfigs.release.keyPassword = keyPassword
    } else {
        buildTypes.release.signingConfig = null
    }
}

configurations {
    all*.exclude group: 'com.google.guava', module: 'guava-jdk5'
    all*.exclude group: 'org.apache.httpcomponents', module: 'httpclient'
}

final DAGGER_VERSION = '2.9'
final BUTTERKNIFE_VERSION = '8.5.1'
final GPS_VERSION = '10.2.4'
final SUPPORT_VERSION = '25.3.1'
final SUPPORT_ANNOTATIONS_VERSION = '26.0.0-alpha1'
final STETHO_VERSION = '1.4.2'
final TESTING_SUPPORT_VERSION = '0.5'

dependencies {
    annotationProcessor "com.google.dagger:dagger-compiler:${DAGGER_VERSION}"
    compile "com.google.dagger:dagger:${DAGGER_VERSION}"

    annotationProcessor "com.jakewharton:butterknife-compiler:${BUTTERKNIFE_VERSION}"
    compile "com.jakewharton:butterknife:${BUTTERKNIFE_VERSION}"

    debugCompile "com.facebook.stetho:stetho:${STETHO_VERSION}"
    debugCompile "com.facebook.stetho:stetho-timber:${STETHO_VERSION}@aar"
    debugCompile 'com.squareup.leakcanary:leakcanary-android:1.5'
    //noinspection GradleCompatible
    debugCompile 'com.android.support:multidex:1.0.1'

    compile 'com.github.rey5137:material:1.2.4'
    compile 'com.nononsenseapps:filepicker:4.0.0'
    compile "com.android.support:design:${SUPPORT_VERSION}"
    compile "com.android.support:support-annotations:${SUPPORT_VERSION}"
    compile "com.android.support:support-v13:${SUPPORT_VERSION}"
    compile "com.android.support:cardview-v7:${SUPPORT_VERSION}"
    compile 'com.jakewharton.timber:timber:4.5.1'
    compile 'com.jakewharton.threetenabp:threetenabp:1.0.5'
    compile 'com.google.guava:guava:20.0'
    compile 'com.jakewharton:process-phoenix:1.1.1'
    compile 'com.google.android.apps.dashclock:dashclock-api:2.0.0'
    compile 'com.twofortyfouram:android-plugin-api-for-locale:1.0.2'
    compile 'com.bignerdranch.android:recyclerview-multiselect:0.2'
    compile ('com.rubiconproject.oss:jchronic:0.2.6') {
        transitive = false
    }
    compile ('org.scala-saddle:google-rfc-2445:20110304') {
        transitive = false
    }
    compile ('com.wdullaer:materialdatetimepicker:3.1.3') {
        exclude group: 'com.android.support', module: 'support-v4'
    }

    googleplayCompile "com.google.android.gms:play-services-location:${GPS_VERSION}"
    googleplayCompile "com.google.android.gms:play-services-analytics:${GPS_VERSION}"
    googleplayCompile "com.google.android.gms:play-services-auth:${GPS_VERSION}"
    googleplayCompile "com.google.android.gms:play-services-places:${GPS_VERSION}"
    googleplayCompile 'com.google.apis:google-api-services-tasks:v1-rev46-1.22.0'
    googleplayCompile 'com.google.api-client:google-api-client-android:1.22.0'

    amazonCompile "com.google.android.gms:play-services-analytics:${GPS_VERSION}"

    androidTestAnnotationProcessor "com.google.dagger:dagger-compiler:${DAGGER_VERSION}"
    androidTestAnnotationProcessor "com.jakewharton:butterknife-compiler:${BUTTERKNIFE_VERSION}"
    androidTestCompile 'com.google.dexmaker:dexmaker-mockito:1.2'
    androidTestCompile 'com.natpryce:make-it-easy:4.0.1'
    androidTestCompile "com.android.support.test:runner:${TESTING_SUPPORT_VERSION}"
    androidTestCompile "com.android.support.test:rules:${TESTING_SUPPORT_VERSION}"
    androidTestCompile "com.android.support:support-annotations:${SUPPORT_ANNOTATIONS_VERSION}"
}
